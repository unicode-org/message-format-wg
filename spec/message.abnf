message = simple-message / complex-message

simple-message = [simple-start pattern]
simple-start = simple-start-char / text-escape / expression
pattern = *(text-char / text-escape / expression)

complex-message = *(declaration [s]) complex-body [s]
declaration = input-declaration / local-declaration / reserved-keyword
input-declaration = input [s] variable-expression
local-declaration = local s variable [s] "=" [s] expression

complex-body = quoted-pattern
             / (selectors 1*([s] variant))
quoted-pattern = "{{" pattern "}}"

selectors = match 1*([s] expression)
variant = key *(s key) [s] quoted-pattern
key = literal / "*"

expression = literal-expression / variable-expression / function-expression
literal-expression = "{" [s] literal [s annotation] [s] "}"
variable-expression = "{" [s] variable [s annotation] [s] "}"
function-expression = "{" [s] annotation [s] "}"
annotation = (function *(s option))
           / reserved-annotation
           / private-use-annotation

literal = quoted / unquoted
variable = "$" name
function = (":" / "+" / "-") name
option = name [s] "=" [s] (literal / variable)

input = %s".input"
local = %s".local"
match = %s".match"

simple-start-char = %x0-2D         ; omit .
                  / %x2F-5B        ; omit \
                  / %x5D-7A        ; omit {
                  / %x7C           ; omit }
                  / %x7E-D7FF      ; omit surrogates
                  / %xE000-10FFFF
text-char = simple-start-char / "."

quoted      = "|" *(quoted-char / quoted-escape) "|"
quoted-char = %x0-5B         ; omit \
            / %x5D-7B        ; omit |
            / %x7D-D7FF      ; omit surrogates
            / %xE000-10FFFF

; based on https://www.w3.org/TR/xml/#NT-Nmtoken,
; but cannot start with U+002D HYPHEN-MINUS or U+003A COLON ":"
unquoted = unquoted-start *name-char
unquoted-start = name-start / DIGIT / "."
               / %xB7 / %x300-36F / %x203F-2040


; Reserve additional .keywords for use by future versions of this specification.
; Note that the following expression is a simplification,
; as this rule MUST NOT be considered to match existing keywords:
;   (input / local / match) (s / "{")
reserved-keyword = "." reserved-body 1*expression

; Reserve additional sigils for use by future versions of this specification.
reserved-annotation = reserved-annotation-start reserved-body
reserved-annotation-start = "!" / "@" / "#" / "%" / "*"
                          / "<" / ">" / "/" / "?" / "~"

reserved-body  = *([s] 1*(reserved-char / reserved-escape / quoted))
reserved-char  = %x00-08        ; omit HTAB and LF
               / %x0B-0C        ; omit CR
               / %x0E-19        ; omit SP
               / %x21-5B        ; omit \
               / %x5D-7A        ; omit { | }
               / %x7E-D7FF      ; omit surrogates
               / %xE000-10FFFF

; Reserve sigils for private-use by implementations.
private-use-annotation = private-start reserved-body
private-start  = "^" / "&"

; based on https://www.w3.org/TR/xml/#NT-Name,
; but cannot start with U+003A COLON ":"
name = name-start *name-char
name-start = ALPHA / "_"
           / %xC0-D6 / %xD8-F6 / %xF8-2FF
           / %x370-37D / %x37F-1FFF / %x200C-200D
           / %x2070-218F / %x2C00-2FEF / %x3001-D7FF
           / %xF900-FDCF / %xFDF0-FFFD / %x10000-EFFFF
name-char  = name-start / DIGIT / "-" / "." / ":"
           / %xB7 / %x300-36F / %x203F-2040

text-escape     = backslash ( backslash / "." / "{" / "}" )
quoted-escape   = backslash ( backslash / "|" )
reserved-escape = backslash ( backslash / "{" / "|" / "}" )
backslash       = %x5C ; U+005C REVERSE SOLIDUS "\"

s = 1*( SP / HTAB / CR / LF )
