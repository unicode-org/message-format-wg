message           = simple-message / complex-message

simple-message    = o [simple-start pattern]
simple-start      = simple-start-char / escaped-char / placeholder
pattern           = *(text-char / escaped-char / placeholder)
placeholder       = expression / markup

complex-message   = o *(declaration o) complex-body o
declaration       = input-declaration / local-declaration
complex-body      = quoted-pattern / matcher

input-declaration = input o variable-expression
local-declaration = local s variable o "=" o expression

quoted-pattern    = "{{" pattern "}}"

matcher           = match-statement s variant *(o variant)
match-statement   = match 1*(s selector)
selector          = variable
variant           = key *(s key) o quoted-pattern
key               = literal / "*"

; Expressions
expression          = literal-expression
                    / variable-expression
                    / function-expression
literal-expression  = "{" o literal [s function] *(s attribute) o "}"
variable-expression = "{" o variable [s function] *(s attribute) o "}"
function-expression = "{" o function *(s attribute) o "}"

markup = "{" o "#" identifier *(s option) *(s attribute) o ["/"] "}"  ; open and standalone
       / "{" o "/" identifier *(s option) *(s attribute) o "}"  ; close

; Expression and literal parts
function       = ":" identifier *(s option)
option         = identifier o "=" o (literal / variable)

attribute      = "@" identifier [o "=" o literal]

variable       = "$" name

literal          = quoted-literal / unquoted-literal
quoted-literal   = "|" *(quoted-char / escaped-char) "|"
unquoted-literal = 1*name-char

; Keywords; Note that these are case-sensitive
input = %s".input"
local = %s".local"
match = %s".match"

; Names and identifiers
identifier = [namespace ":"] name
namespace  = name
name       = [bidi] name-start *name-char [bidi]
name-start = ALPHA
                  / %x2B          ; 【+】     omit Cc %x0-1F, Whitespace %20, Ascii 【!"#$%&'()*】
                  / %x5F          ; 【_】     omit Ascii 【,-./0123456789:;<=>?@】 【[\]^】
                  / %xA1-61B      ;          omit Cc %x7F-9F, Whitespace %xA0, Ascii 【`】 【{|}~】
                  / %x61D-167F    ;          omit BidiControl %x61C
                  / %x1681-1FFF   ;          omit Whitespace %x1680
                  / %x200B-200D   ;          omit Whitespace %x2000-200A
                  / %x2010-2027   ;          omit BidiControl %x200E-200F
                  / %x2030-205E   ;          omit Whitespace %x2028-2029 %x202F, BidiControl %x202A-202E
                  / %x2060-2065   ;          omit Whitespace %x205F
                  / %x206A-2FFF   ;          omit BidiControl %x2066-2069
                  / %x3001-D7FF   ;          omit Whitespace %x3000
                  / %xF900-FDCF   ;          omit Cs %xD800-DFFF, Co %xE000-F8FF
                  / %xFDF0-FFFD   ;          omit NChar %xFDD0-FDEF
                  / %x10000-1FFFD ;          omit NChar %xFFFE-FFFF
                  / %x20000-2FFFD ;          omit NChar %x1FFFE-1FFFF
                  / %x30000-3FFFD ;          omit NChar %x2FFFE-2FFFF
                  / %x40000-4FFFD ;          omit NChar %x3FFFE-3FFFF
                  / %x50000-5FFFD ;          omit NChar %x4FFFE-4FFFF
                  / %x60000-6FFFD ;          omit NChar %x5FFFE-5FFFF
                  / %x70000-7FFFD ;          omit NChar %x6FFFE-6FFFF
                  / %x80000-8FFFD ;          omit NChar %x7FFFE-7FFFF
                  / %x90000-9FFFD ;          omit NChar %x8FFFE-8FFFF
                  / %xA0000-AFFFD ;          omit NChar %x9FFFE-9FFFF
                  / %xB0000-BFFFD ;          omit NChar %xAFFFE-AFFFF
                  / %xC0000-CFFFD ;          omit NChar %xBFFFE-BFFFF
                  / %xD0000-DFFFD ;          omit NChar %xCFFFE-CFFFF
                  / %xE0000-EFFFD ;          omit NChar %xDFFFE-DFFFF,
                                  ;          omit NChar %xEFFFE-EFFFF %xFFFFE-FFFFF %x10FFFE-10FFFF,
                                  ;          omit Co %xF0000-FFFFD %x100000-10FFFD

name-char  = name-start / DIGIT
                  / %x2D-2E       ; 【-.】    omit Cc %x0-1F, Whitespace 【 】, Ascii 【!"#$%&'()*+,】

; Restrictions on characters in various contexts
simple-start-char = %x01-08        ; omit NULL (%x00), HTAB (%x09) and LF (%x0A)
                  / %x0B-0C        ; omit CR (%x0D)
                  / %x0E-1F        ; omit SP (%x20)
                  / %x21-2D        ; omit . (%x2E)
                  / %x2F-5B        ; omit \ (%x5C)
                  / %x5D-7A        ; omit { (%x7B)
                  / %x7C           ; omit } (%x7D)
                  / %x7E-2FFF      ; omit IDEOGRAPHIC SPACE (%x3000)
                  / %x3001-10FFFF
text-char         = %x01-5B        ; omit NULL (%x00) and \ (%x5C)
                  / %x5D-7A        ; omit { (%x7B)
                  / %x7C           ; omit } (%x7D)
                  / %x7E-10FFFF
quoted-char       = %x01-5B        ; omit NULL (%x00) and \ (%x5C)
                  / %x5D-7B        ; omit | (%x7C)
                  / %x7D-10FFFF

; Character escapes
escaped-char = backslash ( backslash / "{" / "|" / "}" )
backslash    = %x5C ; U+005C REVERSE SOLIDUS "\"

; Required whitespace
s = *bidi ws o

; Optional whitespace
o = *(ws / bidi)

; Bidirectional marks and isolates
; ALM / LRM / RLM / LRI, RLI, FSI & PDI
bidi = %x061C / %x200E / %x200F / %x2066-2069

; Whitespace characters
ws = SP / HTAB / CR / LF / %x3000
